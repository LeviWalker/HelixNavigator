/*
 * The build configuration of HelixNavigator. This includes options to build
 * excecutables with launch4j and jpackage.
 */

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'edu.sc.seis.launch4j' version '2.5.1'
}

version = '0.0.0'
def applicationName = rootProject.name

archivesBaseName = applicationName
def packageName = "${applicationName}-${version}"

mainClassName = 'org.team2363.helixnavigator.Main'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.jlbabilino:json:master-SNAPSHOT'
}

application {
    mainModule = 'org.team2363.helixnavigator'
    mainClass = mainClassName
    applicationName = project.applicationName
}

javafx {
    version = "17"
    modules = [ 'javafx.controls' ]
}

shadowJar {
    // minimize()

    archiveClassifier.set('')
}

launch4j {
    mainClassName = 'org.team2363.helixnavigator.Main'
    version = project.version
    outfile = "${packageName}.exe"
    bundledJrePath = 'C:\\Program Files\\Java\\jdk-17.0.2\\bin'
    jarTask = project.tasks.shadowJar
}

task createDmg {
    dependsOn shadowJar
    
    doLast {
        def jarDir = "${buildDir}/libs"
        def jarName = "${packageName}.jar"
        def jarFile = "${buildDir}/libs/${jarName}"
        def dest = "${buildDir}/package"
        project.exec {
            commandLine('jpackage',
                    '--input', jarDir,
                    '--main-jar', jarName,
                    '--dest', dest,
                    '--name', rootProject.name,
                    // '--app-version', project.version
                    )
        }
    }
}